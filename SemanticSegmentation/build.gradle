plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk Integer.valueOf(androidCompileSDK)

    defaultConfig {
        applicationId "com.quicinc.semanticsegmentation"
        minSdk Integer.valueOf(androidMinSDK)
        targetSdk Integer.valueOf(androidTargetSDK)
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resValue("string", "tfLiteModelAsset", project.properties['semanticsegmentation_tfLiteModelAsset'])
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.valueOf("VERSION_$javaSourceCompatibilityVersion")
        targetCompatibility JavaVersion.valueOf("VERSION_$javaTargetCompatibilityVersion")
    }

    kotlinOptions {
        jvmTarget = "${javaTargetCompatibilityVersion}"
    }

    // Helper sources are colocated under this module's java/ directory

    preBuild.doFirst {
        if (!file("./src/main/assets/" + project.properties["semanticsegmentation_tfLiteModelAsset"]).exists()) {
            throw new RuntimeException(missingModelErrorMsg)
        }
    }

    packaging {
        resources {
            excludes += "/META-INF/**"
        }
    }

    namespace 'com.quicinc.semanticsegmentation'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.24"
    api 'org.tensorflow:tensorflow-lite:2.16.1'
    implementation 'org.tensorflow:tensorflow-lite-gpu:2.16.1'
    implementation 'org.tensorflow:tensorflow-lite-gpu-api:2.16.1'
    implementation 'org.tensorflow:tensorflow-lite-gpu-delegate-plugin:0.4.4'
    implementation "com.qualcomm.qti:qnn-runtime:$qnnVersion"
    implementation "com.qualcomm.qti:qnn-litert-delegate:$qnnVersion"
    implementation 'androidx.fragment:fragment-ktx:1.8.0'

    // Keep Android OpenCV (used by TfLiteSegmentor's org.opencv.android.Utils)
    implementation 'org.opencv:opencv:4.10.0'

    // Use javacv-platform but exclude its OpenCV presets to avoid duplicate OpenCV versions
    implementation('org.bytedeco:javacv-platform:1.5.8') {
        // Remove javacv's bundled OpenCV to prevent duplicate-version conflicts with org.opencv:opencv
        exclude group: 'org.bytedeco', module: 'opencv'
        exclude group: 'org.bytedeco', module: 'opencv-platform'
    }
}

if (System.getProperty("user.dir") != project.rootDir.path) {
    throw new RuntimeException("This project should be opened from the `android` directory (parent of SemanticSegmentation directory), NOT the SemanticSegmentation directory.")
}
